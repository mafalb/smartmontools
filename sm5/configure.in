#
# $Id: configure.in,v 1.51 2004/02/12 20:33:34 ballen4705 Exp $
#
dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.50)
AC_INIT(smartmontools, 5.27, smartmontools-support@lists.sourceforge.net)
AC_CONFIG_SRCDIR(smartctl.c)

smartmontools_configure_date=`date -u +"%Y/%m/%d %T %Z"`
smartmontools_cvs_tag=`echo '$Id: configure.in,v 1.51 2004/02/12 20:33:34 ballen4705 Exp $'`
smartmontools_release_date=2004/02/12
smartmontools_release_time="20:36:59 UTC"

AC_DEFINE_UNQUOTED(SMARTMONTOOLS_CONFIGURE_ARGS, "$ac_configure_args",            [smartmontools Configure Arguments])
AC_DEFINE_UNQUOTED(SMARTMONTOOLS_CONFIGURE_DATE, "$smartmontools_configure_date", [smartmontools Configure Date])
AC_DEFINE_UNQUOTED(SMARTMONTOOLS_RELEASE_DATE,   "$smartmontools_release_date",   [smartmontools Release Date])
AC_DEFINE_UNQUOTED(SMARTMONTOOLS_RELEASE_TIME,   "$smartmontools_release_time",   [smartmontools Release Time])
AC_DEFINE_UNQUOTED(CONFIG_H_CVSID,               "$smartmontools_cvs_tag",        [smartmontools CVS Tag])
AC_DEFINE_UNQUOTED(PACKAGE_HOMEPAGE,             "http://smartmontools.sourceforge.net/", [smartmontools Home Page])

AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE

AM_MAINTAINER_MODE

AC_LANG_C
dnl Checks for programs.
AC_PROG_CC
AM_PROG_AS
AC_PROG_INSTALL

dnl Checks for libraries.

dnl Checks for header files.
AC_CHECK_HEADER([getopt.h])
AC_CHECK_HEADERS([linux/hdreg.h])

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS([getopt])
AC_CHECK_FUNCS([getopt_long])
AC_CHECK_FUNCS([getdomainname])
AC_CHECK_FUNCS([sigset])
AC_CHECK_FUNCS([uname])

AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(ASFLAGS)

AC_PREFIX_DEFAULT(['/usr'])
AC_SUBST([exampledir], ['${docdir}/examplescripts'])

AC_ARG_WITH(initscriptdir,[AC_HELP_STRING([--with-initscriptdir=dir],[Location of init scripts (default is ${sysconfdir}/rc.d/init.d)])],[initddir="$withval"],[initddir='${sysconfdir}/rc.d/init.d'])
AC_SUBST(initddir)

AC_ARG_WITH(docdir,[AC_HELP_STRING([--with-docdir=dir],[Location of documentation (default is ${prefix}/share/doc/smartmontools-5.X)])],[docdir="$withval"],[docdir='${prefix}/share/doc/${PACKAGE}-${VERSION}'])
AC_SUBST(docdir)

AC_ARG_ENABLE(sample,[AC_HELP_STRING([--enable-sample],[Enables appending .sample to the installed smartd rc script and configuration file])],[smartd_suffix='.sample'],[smartd_suffix=''])
AC_SUBST(smartd_suffix)
AM_CONDITIONAL(SMARTD_SUFFIX, test $smartd_suffix)

if test "$prefix" = "NONE"; then
    dnl no prefix and no sysconfdir, so default to /etc
    if test "$sysconfdir" = '${prefix}/etc'; then
        AC_SUBST([sysconfdir], ['/etc'])
    fi
    dnl no prefix and no mandir, so use ${prefix}/share/man as default
    if test "$mandir" = '${prefix}/man'; then
    	AC_SUBST([mandir], ['${prefix}/share/man'])
    fi
fi

AC_SUBST(releaseversion,['${PACKAGE}-${VERSION}'])
AC_SUBST(smartmontools_release_date)
AC_SUBST(smartmontools_release_time)

AC_CANONICAL_HOST
dnl if OS not recognized, then use the os_generic modules
case "${host}" in
	*-*-linux-gnu*) 
		AC_SUBST([os_deps], ['os_linux.o']) 
		AC_SUBST([os_libs], ['']) ;;
	*-*-linux*)
		AC_SUBST([os_deps], ['os_linux.o']) 
		AC_SUBST([os_libs], ['']) ;;
	*-*-freebsd*)
		AC_SUBST([os_deps], ['os_freebsd.o']) 
		AC_SUBST([os_libs], ['-lcam']);;
	sparc-*-solaris*) 
                AC_DEFINE_UNQUOTED(DEFAULT_MAILER, "mailx", [use mailx as default mailer]) 
		AC_SUBST([os_deps], ['os_solaris.o os_solaris_ata.o']) 
		AC_SUBST([os_libs], ['']) ;;
	pc-*-solaris*) 
                AC_DEFINE_UNQUOTED(DEFAULT_MAILER, "mailx", [use mailx as default mailer]) 
		AC_SUBST([os_deps], ['os_solaris.o']) 
		AC_SUBST([os_libs], ['']) ;;
	*-*-netbsd*)
		AC_SUBST([os_deps], ['os_netbsd.o']) 
		AC_SUBST([os_libs], ['-lutil']) ;;
	*)
		AC_SUBST([os_deps], ['os_generic.o']) 
		AC_SUBST([os_libs], ['']) ;;
esac

dnl Define platform-specific symbol.
AM_CONDITIONAL(OS_SOLARIS, [echo $host_os | grep '^solaris*' > /dev/null])

dnl Add -Wall and -W if using gcc and its not already specified.
if test "x$GCC" = "xyes"; then
  if test -z "`echo "$CFLAGS" | grep "\-Wall" 2> /dev/null`" ; then
      CFLAGS="$CFLAGS -Wall"
  fi
# In the next line, do NOT delete the 2 spaces inside double quotes.
  if test -z "`echo "$CFLAGS " | grep "\-W " 2> /dev/null`" ; then
      CFLAGS="$CFLAGS -W"
  fi
else
 dnl We are NOT using gcc, so enable host-specific compiler flags
 case "${host}" in
	*-*-solaris*) 
          dnl set CFLAGS for Solaris C compiler
          if test -z "`echo "$CFLAGS" | grep "\-xmemalign" 2> /dev/null`" ; then
            dnl we have to tell the compilers about packed ATA structures
            CFLAGS="-xmemalign=1i $CFLAGS"
          fi
          if test -z "`echo "$CFLAGS" | grep "\-xCC" 2> /dev/null`" ; then
            dnl we have to tell the compiler to ignore C++ style comments
            CFLAGS="-xCC $CFLAGS"
          fi
          if test -z "`echo "$CFLAGS" | grep "\-xO" 2> /dev/null`" ; then
            dnl turn on optimization if user has not explicitly set its value
            CFLAGS="-xO2 $CFLAGS"
          fi
 esac
fi

AC_DEFINE_UNQUOTED(SMARTMONTOOLS_BUILD_HOST,     "${host}",                       [smartmontools Build Host])
AC_DEFINE_UNQUOTED(SMARTMONTOOLS_SYSCONFDIR,     "${sysconfdir}",                 [smartmontools System Configuration Directory])

AC_SUBST(CFLAGS)

AC_OUTPUT(Makefile examplescripts/Makefile smartd.initd)
AC_PROG_MAKE_SET
